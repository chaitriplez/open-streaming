plugins {
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id "com.cinnober.gradle.semver-git" version "2.5.0"
}

group = 'com.github.chaitriplez'
sourceCompatibility = '1.8'

repositories {
    jcenter()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    implementation 'com.squareup.retrofit2:retrofit:2.7.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.7.0'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.14.4'

    implementation "io.springfox:springfox-swagger2:2.9.2"
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'
    implementation "io.springfox:springfox-bean-validators:2.9.2"

    implementation "io.github.resilience4j:resilience4j-ratelimiter:1.2.0"
    implementation "io.github.resilience4j:resilience4j-retrofit:1.2.0"

    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.2'

    compileOnly 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.projectlombok:lombok:1.18.10'

    runtimeOnly files('local-conf')

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

bootJar.enabled = false
jar.enabled = true

test {
    useJUnitPlatform()
}

task copyDependencies(type: Copy) {
    from configurations.runtimeClasspath
    include '*.jar'
    into "${buildDir}/dependencies"
}

task initialDependencies {
    doLast {
        for (file in configurations.testRuntimeClasspath) {
            println "File: ${file.name} ${file.size() / 1000} kB"
        }
    }
}